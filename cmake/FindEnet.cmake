# - Try to find ENET
# Once done this will define
#
#  ENET_FOUND - system has ENET
#  ENET_INCLUDE_DIR - the ENET include directory
#  ENET_LIBRARIES - Link these to use ENET
# Redistribution and use is allowed according to the terms of the BSD license.
# For details see the accompanying COPYING-CMAKE-SCRIPTS file.
#

set(ENET_INCLUDE $ENV{ENET_INCLUDE} CACHE STRING "Enet include directory path")
set(ENET_LIB $ENV{ENET_LIB} CACHE STRING "Enet library directory path")

if ( ENET_INCLUDE_DIR AND ENET_LIBRARIES )
	# in cache already
	SET(ENET_FIND_QUIETLY TRUE)
endif ( ENET_INCLUDE_DIR AND ENET_LIBRARIES )

# use pkg-config to get the directories and then use these values
# in the FIND_PATH() and FIND_LIBRARY() calls
if( NOT WIN32 )
	find_package(PkgConfig)

	pkg_check_modules(ENET_PKG QUIET ENET)
endif( NOT WIN32 )

set(ENET_LIB_NAME enet)
if(MSVC)
	if(CMAKE_SIZEOF_VOID_P EQUAL 8)
		set(NUGET_ENET_LIBS_DIR ${CMAKE_BINARY_DIR}/packages/unofficial.flayan.enet/build/native/lib/x64)
		set(ENET_LIB_NAME enet64)
	else()
		set(NUGET_ENET_LIBS_DIR ${CMAKE_BINARY_DIR}/packages/unofficial.flayan.enet/build/native/lib/Win32)
	endif()
endif()

FIND_PATH(ENET_INCLUDE_DIR NAMES enet/enet.h
	PATHS
		${ENET_INCLUDE}
		/usr/local/include
		/usr/X11/include
		/usr/include
		${CMAKE_BINARY_DIR}/packages/unofficial.flayan.enet/build/native/include
		${OSX_TOOLCHAIN_PKGS}/include
	HINTS
		${ENET_PKG_INCLUDE_DIRS} # Generated by pkg-config
	CMAKE_FIND_ROOT_PATH_BOTH
)

FIND_LIBRARY(ENET_LIBRARIES NAMES ${ENET_LIB_NAME} ${ENET_PKG_LIBRARIES}
	PATHS
		${ENET_LIB}
		/usr/local
		/usr/X11
		/usr
		${NUGET_ENET_LIBS_DIR}
		${OSX_TOOLCHAIN_PKGS}
	HINTS
		${ENET_PKG_LIBRARY_DIRS} # Generated by pkg-config
	PATH_SUFFIXES
		lib64
		lib
	CMAKE_FIND_ROOT_PATH_BOTH
)

get_filename_component(ENET_LIBRARY_DIR ${ENET_LIBRARIES} DIRECTORY)

include(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(ENET DEFAULT_MSG ENET_LIBRARIES ENET_INCLUDE_DIR )

# show the ENET_INCLUDE_DIR and ENET_LIBRARIES variables only in the advanced view
MARK_AS_ADVANCED(ENET_INCLUDE_DIR ENET_LIBRARIES )
